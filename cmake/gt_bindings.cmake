# Create a library with c- and Fortran-bindings
#
# Usage of this module:
#
#  add_bindings_library(<library-name> SOURCES <sources>[...] [FORTRAN_OUTPUT_DIR fortran_dir] [C_OUTPUT_DIR c_dir] [FORTRAN_MODULE_NAME name])
#
#  Arguments:
#   SOURCES: sources of the library
#   FORTRAN_OUTPUT_DIR: destination for generated Fortran files (default: ${CMAKE_CURRENT_LIST_DIR})
#   C_OUTPUT_DIR: destination for generated C files (default: ${CMAKE_CURRENT_LIST_DIR})
#   FORTRAN_MODULE_NAME: name for the Fortran module (default: <library-name>)
#
# Variables used by this module:
#
#  GT_BINDINGS_CROSS_COMPILATION:
#  If GT_BINDINGS_CROSS_COMPILATION=ON, bindings will not be generated, but expected to be provided,
#  as part of the user source code, e.g. by updating bindings with the bindings generator during development.
#  If GT_BINDINGS_CROSS_COMPILATION is not defined already it will be made available after including this file.
#  Including this module will make the cached CMake variable GT_BINDINGS_CROSS_COMPILATION available.
#
# In the default case (GT_BINDINGS_CROSS_COMPILATION=OFF), the bindings files are generated in the directory
# where the CMakeLists.txt with the call to add_bindings_library() is located.
#
# Targets generated by add_bindings_library(<library-name> ...):
#  - <library_name> library build from <Sources...> without bindings (ususally this target is not used)
#  - <library_name>_declarations will run the generator for this library
#  - <library_name>_c the C-bindings with <library_name> linked to it
#  - <library_name>_fortran the Fortran-bindings with <library_name> linked to it


option(GT_BINDINGS_CROSS_COMPILATION "If turned on, bindings will not be generated." OFF)

if(DEFINED GRIDTOOLS_LIBRARIES_DIR)
    set(local_GRIDTOOLS_ROOT ${GRIDTOOLS_LIBRARIES_DIR}/..)
else()
    set(local_GRIDTOOLS_ROOT ${CMAKE_SOURCE_DIR})
endif()

if(NOT TARGET Boost::boost)
    find_package(Boost)
endif()

add_library(c_bindings_generator ${local_GRIDTOOLS_ROOT}/src/c_bindings/generator.cpp ${local_GRIDTOOLS_ROOT}/src/c_bindings/generator_main.cpp)
target_include_directories(c_bindings_generator PUBLIC $<BUILD_INTERFACE: ${local_GRIDTOOLS_ROOT}/include> $<INSTALL_INTERFACE:include>)
target_link_libraries(c_bindings_generator Boost::boost)

add_library(c_bindings_handle ${local_GRIDTOOLS_ROOT}/src/c_bindings/handle.cpp)
target_include_directories(c_bindings_handle PUBLIC $<BUILD_INTERFACE: ${local_GRIDTOOLS_ROOT}/include> $<INSTALL_INTERFACE:include>)

# enable_bindings_library_fortran()
#
# Create a target to compile the generated Fortran module.
# In the default case, when Fortran is enabled on the call to add_bindings_library(), this target is automatically created.
# In case when the Fortran language was not enabled, we cannot create a library (add_library()) with Fortran files.
# However if the user wants to use the target at a later stage, e.g. in testing (with Fortran enabled), the target can
# be created by a call to enable_bindings_library_fortran().
macro(enable_bindings_library_fortran target_name)
    get_property(languages GLOBAL PROPERTY ENABLED_LANGUAGES)
    cmake_policy(PUSH)
    cmake_policy(SET CMP0057 NEW) # some versions of CMake require this to use IN_LIST
    if("Fortran" IN_LIST languages)
        if(NOT TARGET fortran_bindings_handle)
            add_library(fortran_bindings_handle ${local_GRIDTOOLS_ROOT}/src/c_bindings/array_descriptor.f90 ${local_GRIDTOOLS_ROOT}/src/c_bindings/handle.f90)
            target_link_libraries(fortran_bindings_handle PUBLIC c_bindings_handle)
            target_include_directories(fortran_bindings_handle PUBLIC ${CMAKE_CURRENT_BINARY_DIR}) #for the .mod files
        endif()
        if(NOT TARGET ${target_name}_fortran)
            set_source_files_properties(${${target_name}_fortran_bindings_path} PROPERTIES GENERATED TRUE)
            add_library(${target_name}_fortran EXCLUDE_FROM_ALL ${${target_name}_fortran_bindings_path})
            target_link_libraries(${target_name}_fortran ${target_name})
            target_link_libraries(${target_name}_fortran fortran_bindings_handle)
            target_include_directories(${target_name}_fortran PUBLIC ${CMAKE_CURRENT_BINARY_DIR}) # location of mod file
            add_dependencies(${target_name}_fortran ${target_name}_declarations)
        endif()
    elseif(NOT ${ARGN}) # internal: the second (optional) parameter can be used to surpress this fatal error
        message(FATAL_ERROR "Please enable_language(Fortran) to compile the Fortran bindings.")
    endif()
    cmake_policy(POP)
endmacro()

macro(add_bindings_library target_name)
    set(options)
    set(one_value_args FORTRAN_OUTPUT_DIR C_OUTPUT_DIR FORTRAN_MODULE_NAME)
    set(multi_value_args SOURCES)
    cmake_parse_arguments(ARG "${options}" "${one_value_args};" "${multi_value_args}" ${ARGN})

    if(NOT DEFINED ARG_FORTRAN_MODULE_NAME)
        set(ARG_FORTRAN_MODULE_NAME ${target_name}) # default value
    endif()

    if(ARG_C_OUTPUT_DIR)
        set(bindings_c_decl_filename ${ARG_C_OUTPUT_DIR}/${target_name}.h)
    else()
        set(bindings_c_decl_filename ${CMAKE_CURRENT_LIST_DIR}/${target_name}.h) # default value
    endif()
    if(ARG_FORTRAN_OUTPUT_DIR)
        set(bindings_fortran_decl_filename ${ARG_FORTRAN_OUTPUT_DIR}/${target_name}.f90)
    else()
        set(bindings_fortran_decl_filename ${CMAKE_CURRENT_LIST_DIR}/${target_name}.f90) # default value
    endif()

    add_library(${target_name} ${ARG_SOURCES})
    target_link_libraries(${target_name} c_bindings_generator)

    if(NOT GT_BINDINGS_CROSS_COMPILATION)
        # generator
        add_executable(${target_name}_decl_generator
            ${local_GRIDTOOLS_ROOT}/src/c_bindings/generator_main.cpp)
        target_link_libraries(${target_name}_decl_generator c_bindings_generator)
    
        if (${APPLE})
            target_link_libraries(${target_name}_decl_generator
                -Wl,-force_load ${target_name})
        else()
            target_link_libraries(${target_name}_decl_generator
                -Xlinker --whole-archive ${target_name}
                -Xlinker --no-whole-archive)
        endif()
    
        add_custom_target(${target_name}_declarations
            ALL
            COMMAND ${CMAKE_COMMAND}
                -DGENERATOR=${CMAKE_CURRENT_BINARY_DIR}/${target_name}_decl_generator
                -DBINDINGS_C_DECL_FILENAME=${bindings_c_decl_filename}
                -DBINDINGS_FORTRAN_DECL_FILENAME=${bindings_fortran_decl_filename}
                -DFORTRAN_MODULE_NAME=${ARG_FORTRAN_MODULE_NAME}
                -P ${local_GRIDTOOLS_ROOT}/cmake/gt_bindings_generate.cmake
            BYPRODUCTS ${bindings_c_decl_filename} ${bindings_fortran_decl_filename}
            DEPENDS $<TARGET_FILE:${target_name}_decl_generator>)
    else()
        if(EXISTS ${bindings_c_decl_filename} AND (EXISTS ${bindings_fortran_decl_filename}))
            add_custom_target(${target_name}_declarations) # noop, the dependencies are satisfied if the files exist
        else()
            message(FATAL_ERROR "Cross-compilation for bindings is enabled: no bindings will be generated, but "
                "${bindings_c_decl_filename} and/or "
                "${bindings_fortran_decl_filename} "
                "are missing. Generate the bindings and consider making them part of your repository.") 
        endif()
    endif()

    # bindings c library
    add_library(${target_name}_c INTERFACE)
    target_link_libraries(${target_name}_c INTERFACE ${target_name})
    target_link_libraries(${target_name}_c INTERFACE c_bindings_handle)
    add_dependencies(${target_name}_c ${target_name}_declarations)

    # bindings Fortran library
    # Export the name of the generated file in this scope and the parent scope.
    # Reason: see description of enable_bindings_library_fortran().
    set(${target_name}_fortran_bindings_path ${bindings_fortran_decl_filename}) # needed if the target is directly created
    set(${target_name}_fortran_bindings_path ${bindings_fortran_decl_filename} PARENT_SCOPE) # if the target is created by the user
    enable_bindings_library_fortran(${target_name} TRUE)
endmacro()
