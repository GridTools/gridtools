cmake_minimum_required(VERSION @CMAKE_MINIMUM_REQUIRED_VERSION@)

project(GridTools-examples CXX)

include(CheckLanguage)
check_language(CUDA)
if(CMAKE_CUDA_COMPILER)
    enable_language(CUDA)
endif()

find_package(GridTools @GridTools_VERSION@ REQUIRED
    HINTS @CMAKE_INSTALL_PREFIX@/lib/cmake)

enable_testing()

set(EXAMPLES_SRCFILES @EXAMPLES_SRCFILES@)
foreach(srcfile IN LISTS EXAMPLES_SRCFILES)
    add_executable(${srcfile}_mc ${srcfile}.cpp)
    target_link_libraries(${srcfile}_mc GridTools::gridtools)
    add_test(NAME ${srcfile}_mc COMMAND $<TARGET_FILE:${srcfile}_mc> 33 44 55)
endforeach()

if(CMAKE_CUDA_COMPILER)
    if(GRIDTOOLS_HAS_TARGET_CUDA)
        foreach(srcfile IN LISTS EXAMPLES_SRCFILES)
            add_executable(${srcfile}_cuda ${srcfile}.cu)
            target_link_libraries(${srcfile}_cuda GridTools::gridtools)
            add_test(NAME ${srcfile}_cuda COMMAND $<TARGET_FILE:${srcfile}_cuda> 33 44 55)
        endforeach()
    else()
        message("A CUDA-capable compiler was found, but GridTools was installed without CUDA support. Skipping the CUDA example...")
    endif()
endif()
