# This file drives compilation of all examples and is installed together with the example sources.
cmake_minimum_required(VERSION 3.12.4)

project(GridTools-examples LANGUAGES CXX)

# enable CUDA if it is found on the system
include(CheckLanguage)
check_language(CUDA)
if(CMAKE_CUDA_COMPILER)
    # 2) Enable the CUDA language if you want to run your code on a CUDA-capable GPU
    enable_language(CUDA)
endif()

include(ExternalProject)

enable_testing()

foreach(ex IN ITEMS @enabled_examples@)
    ExternalProject_Add(${ex}-example
        DOWNLOAD_COMMAND ""
        SOURCE_DIR ${CMAKE_CURRENT_LIST_DIR}/${ex}
        CMAKE_ARGS
            -DCMAKE_CXX_COMPILER=${CMAKE_CXX_COMPILER}
            -DCMAKE_CUDA_HOST_COMPILER=${CMAKE_CUDA_HOST_COMPILER}
            -DCMAKE_FIND_PACKAGE_NO_PACKAGE_REGISTRY=ON
        INSTALL_COMMAND ""
        )
    ExternalProject_Get_property(${ex}-example BINARY_DIR)
    add_test(NAME ${ex}-example COMMAND ctest WORKING_DIRECTORY ${BINARY_DIR})
endforeach()
    